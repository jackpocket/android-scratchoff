if (System.getenv("LIBRARY_RELEASE_MODE") != "true") {
    return
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    mavPublishGroupId = "com.jackpocket"
    mavProjectName = "scratchoff"
    mavLibraryDescription = "A Scratchoff View system."
    mavLibraryVersion = "3.0.0"

    mavLicenseName = "The Apache License, Version 2.0"
    mavLicenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"

    mavDeveloperId = "jackpocket"
    mavDeveloperName = "Jackpocket"
    mavDeveloperEmail = "services@jackpocket.com"
    mavDeveloperOrg = "Jackpocket"
    mavDeveloperOrgUrl = "https://jackpocket.com"

    mavSiteUrl = "https://github.com/jackpocket/android-scratchoff"
    mavGitUrl = "https://github.com/jackpocket/android-scratchoff.git"
    mavScmConnection = "scm:git:git://github.com:jackpocket/android-scratchoff.git"
    mavScmDeveloperConnection = "scm:git:ssh://github.com:jackpocket/android-scratchoff.git"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = mavPublishGroupId
                artifactId = mavProjectName
                version = mavLibraryVersion

                pom {
                    name = mavProjectName
                    description = mavLibraryDescription
                    url = mavSiteUrl
                    licenses {
                        license {
                            name = mavLicenseName
                            url = mavLicenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = mavDeveloperId
                            name = mavDeveloperName
                            email = mavDeveloperEmail
                            organization = mavDeveloperOrg
                            organizationUrl = mavDeveloperOrgUrl
                        }
                    }
                    scm {
                        connection = mavScmConnection
                        developerConnection = mavScmDeveloperConnection
                        url = mavGitUrl
                    }
                }
            }
        }

        repositories {
            maven {
                name = 'mavenCentral'
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                credentials {
                    username "${System.getenv("MAVEN_CENTRAL_USER")}"
                    password "${System.getenv("MAVEN_CENTRAL_PASSWORD")}"
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
            System.getenv("RELEASE_SIGNING_KEY_ID"),
            System.getenv("RELEASE_SIGNING_KEY"),
            System.getenv("RELEASE_SIGNING_KEY_PASSWORD")
        )

        sign publishing.publications
    }
}
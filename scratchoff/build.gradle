apply plugin: 'com.android.library'

// Kotlin is only used for androidTestImplementation, but applying
// the plugin will add a compile dependency for the kotlin-stdlib
// to the release publication.
def kolinEnabledVariant = getGradle()
        .getStartParameter()
        .getTaskRequests()
        .toString()
        .contains("Debug")

if (kolinEnabledVariant) {
    apply plugin: 'kotlin-android'
}

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion rootProject.ext.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes.debug.testCoverageEnabled true
}

dependencies {
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test:core:1.3.0"
}

apply from: "./publishing.gradle"